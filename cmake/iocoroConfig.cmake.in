@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(Threads)

set(IOCORO_BUILT_WITH_URING @IOCORO_BUILT_WITH_URING@)
if(IOCORO_BUILT_WITH_URING AND NOT TARGET liburing::liburing)
  find_path(_iocoro_liburing_include_dir NAMES liburing.h)
  find_library(_iocoro_liburing_library NAMES uring)

  if(_iocoro_liburing_include_dir AND _iocoro_liburing_library)
    add_library(liburing::liburing UNKNOWN IMPORTED)
    set_target_properties(liburing::liburing PROPERTIES
      IMPORTED_LOCATION "${_iocoro_liburing_library}"
      INTERFACE_INCLUDE_DIRECTORIES "${_iocoro_liburing_include_dir}"
    )
  else()
    message(FATAL_ERROR
      "iocoro was built with io_uring support, but liburing was not found. "
      "Install liburing (headers + library) or rebuild iocoro with -DIOCORO_ENABLE_URING=OFF."
    )
  endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/iocoroTargets.cmake")

check_required_components(iocoro)


